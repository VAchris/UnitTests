ZZRGUTCM ;RGI/CBR - Unit Tests - Common functions ;4/24/12
 ;;1.0;UNIT TEST;;Apr 25, 2012;Build 1;
NEWPAT ;
 N PAT,PATU,PATDFN,ERR
 S PAT(.01)="GMPLTEST, PATIENT"
 S PAT(.02)="M"
 S PAT(.03)=2820226
 S PAT(.09)="000000121"
 S PAT(.096)=1
 S PAT(.097)=$P($$HTFM^XLFDT($H),".")
 S PAT(.301)="Y"
 S PAT(391)=1
 S PAT(1901)="Y"
 M PATU(2,"+1,")=PAT
 D UPDATE^DIE("","PATU","PATDFN","ERR")
 S GMPDFN=+$G(PATDFN(1))
 Q
 ;
NISTPRBS(PROB1,PROB2) ;
 K PROB1,PROB2
 S PROB1(.01)="9128^496"
 S PROB1(.02)=GMPDFN
 S PROB1(.05)="^Chronic Airway Obstruction, Not Elsewhere Classified"
 S PROB1(.12)="A^ACTIVE"
 S PROB1(.13)=3100404 ; April 4,2010
 S PROB1(1.08)=GMPLCLIN
 S PROB1(1.14)="A^ACUTE"
 S PROB1(1.10)="0^NO"
 S PROB1(10,"NEW")=1
 S PROB1(10,"NEW",1)="testcomment3"
 S PROB2(.01)="2477^401.9"
 S PROB2(.02)=GMPDFN
 S PROB2(.05)="^Essential Hypertension"
 S PROB2(.12)="A^ACTIVE"
 S PROB2(.13)=3100330 ; March 30,2010
 S PROB2(1.08)=GMPLCLIN
 S PROB2(1.14)="C^CHRONIC"
 S PROB2(1.10)="0^NO"
 S PROB2(10,"NEW")=1
 S PROB2(10,"NEW",1)="testcomment4"
 Q
 ;
CHKPRB(IFN,FIELDS,CONDT) ;
 N DBFLDS,FLDNO
 S FLDNO=""
 D DETAIL^GMPLAPI2(.DBFLDS,IFN)
 F  S FLDNO=$O(FIELDS(FLDNO)) Q:FLDNO=""  D
 . I FLDNO=.05 D CHKEQ^XTMUNIT($P(FIELDS(FLDNO),U,2),$P(DBFLDS(FLDNO),U,2),"Invalid value in field: "_FLDNO) Q
 . Q:FLDNO=10
 . D CHKEQ^XTMUNIT($P(FIELDS(FLDNO),U),$P(DBFLDS(FLDNO),U),"Invalid value in field: "_FLDNO)
 I $D(CONDT) D CHKEQ^XTMUNIT(CONDT,$P(DBFLDS(1.02),U))
 Q
 ;
ORARY(PROBS,ARY) ;
 N FLDNO,I
 K ARY
 S FLDNO="",I=1
 F  S FLDNO=$O(PROBS(FLDNO)) Q:FLDNO=""  D
 . Q:FLDNO=10
 . S ARY(I)="GMPFLD("_FLDNO_")="""_PROBS(FLDNO)_""""
 . S I=I+1
 I $D(PROBS(10,"NEW")) D
 . S ARY(I)="GMPFLD(10,""NEW"")="_PROBS(10,"NEW")
 . F FLDNO=1:1:PROBS(10,"NEW") D
 .. S ARY(I)="GMPFLD(10,""NEW"","_FLDNO_")="""_PROBS(10,"NEW",FLDNO)_""""
 Q
 ;
LOGON ;
 S DUZ=$$CHECKAV^XUSRB("fakedoc1;1Doc!@#$")
 D DUZ^XUS1A
 S GMPVAMC=DUZ(2)
 Q
 ;
