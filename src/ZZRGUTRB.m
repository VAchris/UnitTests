ZZRGUTRB ;RGI/CBR - Unit Tests - RPC Broker ;4/24/12
 ;;1.0;UNIT TEST;;Apr 25, 2012;Build 1
 I $T(EN^XTMUNIT)'="" D EN^XTMUNIT("ZZRGUTRB")
 Q
 ;
STARTUP ;
 N GMPIFN1,GMPIFN2
 S DTIME=500 ;D INIT^GMPLMGR
 K XUSER,XOPT
 D LOGON^ZZRGUTCM
 S GMPROV=1
 S DT=$P($$HTFM^XLFDT($H),".")
 S GMPLCLIN=1
 S U="^"
 TSTART
 D NEWPAT^ZZRGUTCM
 Q
 ;
SHUTDOWN ;
 TROLLBACK
 Q
 ;
ADDSAVE ;
 N PROB1,PROB2,FLDS
 D NISTPRBS^ZZRGUTCM(.PROB1,.PROB2)
 D ORARY^ZZRGUTCM(.PROB1,.FLDS)
 D ADDSAVE^ORQQPL1(.GMPIFN1,GMPDFN,GMPROV,GMPVAMC,.FLDS)
 D CHKTF^XTMUNIT(GMPIFN1>0,"NEW^GMPLSAVE: save failed ("_$G(GMPIFN2(0))_")")
 D:GMPIFN1 CHKPRB^ZZRGUTCM(GMPIFN1,.PROB1,"P")
 K FLDS
 D ORARY^ZZRGUTCM(.PROB2,.FLDS)
 D ADDSAVE^ORQQPL1(.GMPIFN2,GMPDFN,GMPROV,GMPVAMC,.FLDS)
 D CHKTF^XTMUNIT(GMPIFN2>0,"NEW^GMPLSAVE: save failed ("_$G(GMPIFN2(0))_")")
 D:GMPIFN2 CHKPRB^ZZRGUTCM(GMPIFN2,.PROB2,"P")
 Q
 ;
PROBL ;
 N LISTP,LISTH
 D PROBL^ORQQPL3(.LISTP,GMPDFN,"A")
 D CHKTF^XTMUNIT(+$G(LISTP(0))=1,"Invalid number of records")
 D CHKEQ^XTMUNIT(GMPIFN1,$P($G(LISTP(1)),U),"Invalid record returned")
 D PROBL^ORQQPL3(.LISTH,GMPDFN,"R")
 D CHKTF^XTMUNIT(+$G(LISTH(0))=1,"Invalid number of records")
 D CHKEQ^XTMUNIT(GMPIFN2,$P($G(LISTH(1)),U),"Invalid record returned")
 Q
 ;
INACT ;
 N RETURN
 D INACT^ORQQPL2(.RETURN,GMPIFN2)
 D CHKTF^XTMUNIT(RETURN>0,"INACT^ORQQPL2 failed: "_$G(RETURN(0)))
 D CHKEQ^XTMUNIT("I",$P(^AUPNPROB(GMPIFN2,0),U,12),"Problem not inactivated ("_GMPIFN2_")")
 Q
 ;
DELETE ;
 N RESULT
 D DELETE^ORQQPL2(.RESULT,GMPIFN2,GMPROV,GMPVAMC,"deletecomment")
 D CHKTF^XTMUNIT(RESULT>0,"DELETE^ORQQPL2 failed: "_$G(RESULT(0)))
 D CHKEQ^XTMUNIT("H",$P(^AUPNPROB(GMPIFN2,1),U,2),"Problem not deleted ("_GMPIFN2_")")
 K RESULT
 D DELETE^ORQQPL2(.RESULT,GMPIFN2,GMPROV,GMPVAMC,"deletecomment1")
 D CHKTF^XTMUNIT(RESULT=0,"Problem deleted twice ("_GMPIFN2_")")
 D CHKEQ^XTMUNIT("PRBDLTD",$P($G(RESULT(0)),U),"Invalid error message")
 Q
 ;
REPLACE ;
 N RETURN
 D REPLACE^ORQQPL2(.RETURN,GMPIFN1)
 D CHKTF^XTMUNIT(RETURN=0,"Error. Replaced an active problem ("_GMPIFN1_")")
 D CHKEQ^XTMUNIT("INVREC",$P($G(RETURN(0)),U),"Invalid error message")
 K RETURN
 D REPLACE^ORQQPL2(.RETURN,GMPIFN2)
 D CHKTF^XTMUNIT(RETURN>0,"REPLACE^ORQQPL2 failed: "_$G(RETURN(0)))
 D CHKEQ^XTMUNIT("P",$P(^AUPNPROB(GMPIFN2,1),U,2),"Problem not replaced ("_GMPIFN2_")")
 Q
 ;
VERIFY ;
 N RETURN
 D VERIFY^ORQQPL2(.RETURN,GMPIFN2)
 D CHKTF^XTMUNIT(RETURN'>0,"Error. Verified a permanent problem ("_GMPIFN2_")")
 D CHKEQ^XTMUNIT("Problem Already Verified",$G(RETURN(0)),"Invalid error message")
 K RETURN
 S $P(^AUPNPROB(GMPIFN2,1),U,2)="T"
 D VERIFY^ORQQPL2(.RETURN,GMPIFN2)
 D CHKTF^XTMUNIT(RETURN>0,"VERIFY^ORQQPL2 failed: "_$G(RETURN(0)))
 D CHKEQ^XTMUNIT("P",$P(^AUPNPROB(GMPIFN2,1),U,2),"Problem not verified ("_GMPIFN2_")")
 Q
 ;
GETCOMM ;
 N ORY
 D GETCOMM^ORQQPL2(.ORY,GMPIFN2)
 D CHKEQ^XTMUNIT(2,ORY,"Invalid number of comments: "_ORY)
 I ORY=1 W !,"Comment found in file: "_ORY(1)
 D:ORY>0 CHKEQ^XTMUNIT("testcomment4",ORY(1),"Invalid comment returned")
 D:ORY>1 CHKEQ^XTMUNIT("deletecomment",ORY(2),"Invalid comment returned")
 Q
 ;
XTENT ;
 ;;ADDSAVE
 ;;INACT
 ;;DELETE
 ;;PROBL
 ;;REPLACE
 ;;VERIFY
 ;;GETCOMM
 Q
 ;
