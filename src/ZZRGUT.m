ZZRGUT ;RGI/CBR - Unit Tests - Problem List API ;4/24/12
 ;;1.0;UNIT TEST;;Apr 25, 2012;Build 1;
 I $T(EN^XTMUNIT)'="" D EN^XTMUNIT("ZZRGUT")
 Q
 ;
STARTUP ;
 N GMPIFN,PAT
 S DTIME=500 ;D INIT^GMPLMGR
 K XUSER,XOPT
 D LOGON^ZZRGUTCM
 S GMPROV=1
 S DT=$P($$HTFM^XLFDT($H),".")
 S GMPLCLIN=1
 S U="^"
 TSTART
 D NEWPAT^ZZRGUTCM
 Q
 ;
SHUTDOWN ;
 TROLLBACK
 Q
 ;
GETPLIST ;
 N GMPLIST,GMPTOTAL,GMPLVIEW,GMPDFN
 S GMPDFN="2^CARTER, DAVID J SR^C0112"
 S GMPLVIEW("ACT")="A"
 S GMPLVIEW("PROV")=0
 S GMPLVIEW("VIEW")=""
 S GMPARAM("VER")=1
 S GMPARAM("PRT")=1
 S GMPARAM("CLU")=1
 S GMPARAM("REV")=0
 S GMPARAM("QUIET")=1
 D GETPLIST^GMPLMGR1(.GMPLIST,.GMPTOTAL,.GMPLVIEW)
 D CHKEQ^XTMUNIT(GMPLIST(0),0,"GETPLIST^GMPLMGR1: Patient David Carter Sr., GMPLIST(0)'=0")
 S GMPDFN="1^CARTER, DAVID J JR^C0111"
 K GMPLIST,GMPTOTAL
 D GETPLIST^GMPLMGR1(.GMPLIST,.GMPTOTAL,.GMPLVIEW)
 D CHKTF^XTMUNIT(GMPLIST(0)'=0,"GETPLIST^GMPLMGR1: Patient David Carter Jr., GMPLIST(0)=0")
 Q
 ;
API2NEW ;
 N GMPFLD,GMPERR,NUM
 S GMPFLD(.01)="872^253.2"
 S GMPFLD(.05)="^Hypopituitarism (Diabetes Insipidus)"
 S GMPFLD(.08)="3120306^3/6/12"
 S GMPFLD(.12)="A^ACTIVE"
 S GMPFLD(.13)="3120101^1/1/12"
 S GMPFLD(1.01)="304837^Hypopituitarism (Diabetes Insipidus)"
 S GMPFLD(1.02)="P"
 S GMPFLD(1.03)=1
 S GMPFLD(1.04)="1^TESTMASTER,USER"
 S GMPFLD(1.05)="1^TESTMASTER,USER"
 S GMPFLD(1.06)=""
 S GMPFLD(1.07)=""
 S GMPFLD(1.08)="4^CLINIC"
 S GMPFLD(1.09)="3120306^3/6/12"
 S GMPFLD(1.1)="0^NO"
 S GMPFLD(1.11)="0^NO"
 S GMPFLD(1.12)="0^NO"
 S GMPFLD(1.13)="0^NO"
 S GMPFLD(1.14)="A^ACUTE"
 S GMPFLD(10,0)=0
 S GMPFLD(10,"NEW",1)="TEST COMMENT"
 D NEW^GMPLAPI2(.GMPIFN,GMPDFN,GMPROV,.GMPFLD)
 I GMPIFN'>0 D FAIL^XTMUNIT("Save failed: "_$G(GMPIFN(0))) Q
 S NUM=$$NEXTNMBR^GMPLDAL(GMPDFN,GMPVAMC)-1
 D CHKEQ^XTMUNIT($G(^AUPNPROB(GMPIFN,0)),"872^"_GMPDFN_"^"_DT_"^^7^"_GMPVAMC_"^"_NUM_"^"_DT_"^^^^A^3120101","Invalid data line 0. IFN: "_$G(GMPIFN))
 D CHKEQ^XTMUNIT($G(^AUPNPROB(GMPIFN,1)),"304837^P^"_DUZ_"^1^1^^^4^"_DT_"^0^0^0^0^A^^^^","Invalid data line 1. IFN: "_$G(GMPIFN,1))
 D CHKEQ^XTMUNIT($G(^AUPNPROB(GMPIFN,11,0)),"^9000011.11PA^1^1","Note facility not saved. IFN: "_$G(GMPIFN,1))
 D CHKTF^XTMUNIT($D(^AUPNPROB(GMPIFN,11,"B",GMPVAMC,1)),"Note facility index not created. IFN: "_$G(GMPIFN,1))
 D CHKEQ^XTMUNIT($G(^AUPNPROB(GMPIFN,11,1,11,0)),"^9000011.1111IA^1^1","Note file empty. IFN: "_$G(GMPIFN,1))
 D CHKEQ^XTMUNIT($G(^AUPNPROB(GMPIFN,11,1,11,1,0)),"1^^TEST COMMENT^A^"_DT_"^1","Invalid note: "_$G(^AUPNPROB(GMPIFN,11,1,11,1,0)))
 D CHKTF^XTMUNIT($D(^AUPNPROB(GMPIFN,11,1,11,"B",1,1)),"Note index not created. IFN: "_$G(GMPIFN,1))
 D CHKTF^XTMUNIT($D(^AUPNPROB("C",304837,GMPIFN)),"C index not updated. IFN: "_$G(GMPIFN,1))
 D CHKTF^XTMUNIT($D(^AUPNPROB("ACTIVE",GMPDFN,"A",GMPIFN)),"A index not updated. IFN: "_$G(GMPIFN,1))
 D CHKTF^XTMUNIT($D(^AUPNPROB("AC",GMPDFN,GMPIFN)),"AC index not updated. IFN: "_$G(GMPIFN,1))
 D CHKTF^XTMUNIT($D(^AUPNPROB("B",872,GMPIFN)),"B index not updated. IFN: "_$G(GMPIFN,1))
 Q
 ;
API2DEL ;
 N OK,NOTENMBR
 K GMPERR
 D DELETE^GMPLAPI2(.OK,GMPIFN,GMPROV,"DELETE TESTS")
 D CHKTF^XTMUNIT(OK,$G(OK(0)))
 D CHKEQ^XTMUNIT("H",$P($G(^AUPNPROB(GMPIFN,1)),U,2),"Record not marked as deleted")
 S NOTENMBR=+$P($G(^AUPNPROB(GMPIFN,11,1,11,0)),U,4)
 D CHKTF^XTMUNIT(NOTENMBR>0,"Delete note not saved.")
 D CHKEQ^XTMUNIT("DELETE TESTS",$P($G(^AUPNPROB(GMPIFN,11,1,11,NOTENMBR,0)),U,3),"Delete note not saved")
 K OK
 D DELETE^GMPLAPI2(.OK,GMPIFN,GMPROV,"DELETE TESTS") ; Should fail with 'Already deleted'
 D CHKTF^XTMUNIT('OK,"Deleted twice")
 Q
 ;
API2UPD ;
 N RETURN
 D UPDATE^GMPLAPI2(RETURN,GMPIFN,GMPORIG,GMPFLD,GMPLUSER,GMPROV)
 Q
 ;
API2DET ;
 N RETURN
 D DETAIL^GMPLAPI2(RETURN,GMPIFN,GMPLMGR,GMPROV)
 Q
 ;
API2DETX ;
 N RETURN
 D DETAILX^GMPLAPI2(RETURN,GMPIFN,GMPLMGR,GMPROV,GMPMULTI)
 Q
 ;
API2VFY ;
 N RETURN
 D VERIFY^GMPLAPI2(RETURN,GMPIFN)
 Q
 ;
API2DUPL ;
 N RETURN
 D DUPL^GMPLAPI2(RETURN,GMPDFN,TERM,TEXT,GMPBOTH)
 Q
 ;
API2DLTD ;
 N RETURN
 D DELETED^GMPLAPI2(RETURN,GMPIFN)
 Q
 ;
API2VFD ;
 N RETURN
 D VERIFIED^GMPLAPI2(.RETURN,GMPIFN)
 Q
 ;
API2ACT1 ;
 N RETURN
 D ACTIVE^GMPLAPI2(.RETURN,GMPIFN)
 D CHKTF^XTMUNIT(RETURN,GMPIFN_" problem should be ACTIVE")
 Q
 ;
API2ACT2 ;
 N RETURN
 D ACTIVE^GMPLAPI2(.RETURN,GMPIFN)
 D CHKTF^XTMUNIT('RETURN,GMPIFN_" problem should be INACTIVE")
 Q
 ;
API2INCT ;
 N RETURN
 D INACTV^GMPLAPI2(.RETURN,GMPIFN,GMPROV,"INACTIVATION NOTE",DT)
 D CHKTF^XTMUNIT(RETURN,$G(RETURN(0)))
 Q
 ;
XTENT ;
 ;;API2NEW
 ;;API2DEL
 ;;API2ACT1
 ;;API2INCT
 ;;API2ACT2
 ;;GETPLIST
 Q
 ;
